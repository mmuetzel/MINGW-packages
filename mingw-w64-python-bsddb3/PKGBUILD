# Contributor: Josip <bpisoj@gmail.com>

_realname=bsddb3
projectname=berkeleydb
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python-${_realname}")
provides=("${MINGW_PACKAGE_PREFIX}-python3-${_realname}")
conflicts=("${MINGW_PACKAGE_PREFIX}-python3-${_realname}")
replaces=("${MINGW_PACKAGE_PREFIX}-python3-${_realname}")
pkgver=18.1.6
pkgrel=1
pkgdesc="Python bindings for Oracle Berkeley DB (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
license=('spdx:BSD-3-Clause')
url="https://www.jcea.es/programacion/pybsddb.htm"
depends=("${MINGW_PACKAGE_PREFIX}-python"
         "${MINGW_PACKAGE_PREFIX}-db")
makedepends=("${MINGW_PACKAGE_PREFIX}-python-build"
             "${MINGW_PACKAGE_PREFIX}-python-installer"
             "${MINGW_PACKAGE_PREFIX}-python-setuptools"
             "${MINGW_PACKAGE_PREFIX}-python-wheel"
             "${MINGW_PACKAGE_PREFIX}-cc")
options=('staticlibs' 'strip' '!debug')
source=(${projectname}-${pkgver}.tar.gz::"https://pypi.python.org/packages/source/b/${projectname}/${projectname}-${pkgver}.tar.gz")
sha256sums=('6d412dd1a5b702aeeda3cbfa10d3399b16a804d016de087234f8579fca613ec9')

prepare() {
  plain "Patching setup files ..."
  cd "${srcdir}/${projectname}-${pkgver}"

  sed -i -e "s|if os.name == 'posix':|if os.name == 'nt':|g" setup3.py
  sed -i -e "s|elif os.name == 'nt':|elif os.name == 'FOO':|g" setup3.py
}

build() {
  cd "${srcdir}"
  cp -r "${projectname}-${pkgver}" "python-build-${MSYSTEM}"
  cd "python-build-${MSYSTEM}"

  CFLAGS+=" -DUNICODE -D_UNICODE"

  export YES_I_HAVE_THE_RIGHT_TO_USE_THIS_BERKELEY_DB_VERSION=YES
  export BERKELEYDB_DIR="${MINGW_PREFIX}"

  ${MINGW_PREFIX}/bin/python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd "${srcdir}/python-build-${MSYSTEM}"

  MSYS2_ARG_CONV_EXCL="--prefix=" \
    ${MINGW_PREFIX}/bin/python -m installer --prefix=${MINGW_PREFIX} \
    --destdir="${pkgdir}" dist/*.whl

  install -Dm644 LICENSE.txt "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname}/LICENSE.txt"
}
