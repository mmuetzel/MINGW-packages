# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Ray Donnelly <mingw.android@gmail.com>
# Contributor: Mateusz Mikula <mati865@gmail.com>

_realname=binutils
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=2.40
pkgrel=4
pkgdesc="A set of programs to assemble and manipulate binary and object files (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64')
url="https://www.gnu.org/software/binutils/"
license=('spdx:GPL-3.0-or-later AND GPL-2.0-or-later AND LGPL-3.0-or-later AND LGPL-2.0-or-later')
groups=("${MINGW_PACKAGE_PREFIX}-toolchain")
depends=("${MINGW_PACKAGE_PREFIX}-zstd")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-autotools")
source=(https://ftp.gnu.org/gnu/binutils/${_realname}-${pkgver}.tar.bz2{,.sig}
        0002-check-for-unusual-file-harder.patch
        0010-bfd-Increase-_bfd_coff_max_nscns-to-65279.patch
        0110-binutils-mingw-gnu-print.patch
        2001-ld-option-to-move-default-bases-under-4GB.patch
        2003-Restore-old-behaviour-of-windres-so-that-options-con.patch
        reproducible-import-libraries.patch
        specify-timestamp.patch
        libiberty-unlink-handle-windows-nul.patch
        bfd-real-fopen-handle-windows-nul.patch
        3001-try-fix-compare_section-abort.patch
        decorated-symbols-in-import-libs.patch
        0001-improve-speed-of-computing-checksums-coff.patch
        0003-make-ar-faster.patch
        0004-add-section-caches-to-coff_data_type.patch
        0005-add-section-caches-to-coff_data_type.patch::"https://sourceware.org/git/?p=binutils-gdb.git;a=commitdiff_plain;h=0cc8cc5e6f82b8d3d8e3803c6f7f5e63f0c866ad;hp=4cb88cfae84363cef6ed59d9d458a20ff2e1e893"
        0006-add-section-caches-to-coff_data_type.patch::"https://sourceware.org/git/?p=binutils-gdb.git;a=commitdiff_plain;h=204d627e83f440dc62871139a688b58c42149f59;hp=92240b19576f2b71719b952f150c8a781e6e5398"
)
sha256sums=('f8298eb153a4b37d112e945aa5cb2850040bcf26a3ea65b5a715c83afe05e48a'
            'SKIP'
            '2c99345fc575c3a060d6677537f636c6c4154fac0fde508070f3b6296c1060d4'
            '4e8ac055df61b1b5d6ae29dc87e1154737c2e87c7b244b44866702cabf1a5d18'
            '5f3fc3949172d2d6e7cd595f9359077e012391a8c3c2aebc02e30fa656ded833'
            '328ed91bdc9c729ab19f2a566d2d1cbb5ac1beb4775bd71d85e9777b4ed4c17a'
            'd584f1cd9e94cba0e9b27625c4acc8ad5242cd625c9b44839d42fc116072568c'
            'a094660ec95996c00b598429843b7869037732146442af567ada9f539bd40480'
            '27696da8ecfff307537a461b205fad44d6abc1fa648fbf839e72a1d3ea71c40a'
            '7ccbd418695733c50966068fa9755a6abb156f53af23701d2bc097c63e9e0030'
            'dda1cf0c1825283a8b3708d1a4087dd650f4fbc3f8aa571e211cfe3e1458c8f8'
            'ddfa01ed6ce1c0608bbcd462ced8383b5f9f686c2b49fb510a41637fff2ca019'
            '6313b7b32840db54bc20cfee7d2537e3e414b657e42e7bae1db1f61d3b63d599'
            'e09ed1265632479e959d6e430e72f45550006dea3881295656ebb562171b44a5'
            'bd65b240b22862700f8fbcc413c3e542b8c383b25d4fd7a1b338cb189b86ddf4'
            'f7429805c457636966d863882e3833ba86be8372b6e36cb61b05dd5c9319f302'
            '8926a24c4109d220da48a4a2e3579bfd41f7bd7e0ea157635882cec772798dda'
            'ad2f429d6928445606c08b290b2154dcafee90a2bfc5947de7510856c8380636')
validpgpkeys=('EAF1C276A747E9ED86210CBAC3126D3B4AE55E93'
              '3A24BC1E8FB409FA9F14371813FCEF89DD9E3C4F')

apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying ${_patch}"
    patch -p1 -i "${srcdir}/${_patch}"
  done
}

prepare() {
  cd ${srcdir}/${_realname}-${pkgver}

  apply_patch_with_msg \
    0002-check-for-unusual-file-harder.patch \
    0010-bfd-Increase-_bfd_coff_max_nscns-to-65279.patch \
    0110-binutils-mingw-gnu-print.patch

  # Add an option to change default bases back below 4GB to ease transition
  # https://github.com/msys2/MINGW-packages/issues/7027
  # https://github.com/msys2/MINGW-packages/issues/7023
  apply_patch_with_msg 2001-ld-option-to-move-default-bases-under-4GB.patch

  # https://github.com/msys2/MINGW-packages/pull/9233#issuecomment-889439433
  patch -R -p1 -i "${srcdir}/2003-Restore-old-behaviour-of-windres-so-that-options-con.patch"

  # patches for reproducibility from Debian:
  # https://salsa.debian.org/mingw-w64-team/binutils-mingw-w64/-/tree/master/debian/patches
  patch -p2 -i "${srcdir}/reproducible-import-libraries.patch"
  patch -p2 -i "${srcdir}/specify-timestamp.patch"

  # Handle Windows nul device
  # https://github.com/msys2/MINGW-packages/issues/1840
  # https://github.com/msys2/MINGW-packages/issues/10520
  # https://github.com/msys2/MINGW-packages/issues/14725

  # https://gcc.gnu.org/bugzilla/show_bug.cgi?id=108276
  # https://gcc.gnu.org/pipermail/gcc-patches/2023-January/609487.html
  patch -p1 -i "${srcdir}/libiberty-unlink-handle-windows-nul.patch"

  # https://sourceware.org/bugzilla/show_bug.cgi?id=29947
  patch -p1 -i "${srcdir}/bfd-real-fopen-handle-windows-nul.patch"

  # https://sourceware.org/bugzilla/show_bug.cgi?id=30079#c2
  # XXX: Update to upstream fix once there is one
  patch -p1 -i "${srcdir}/3001-try-fix-compare_section-abort.patch"

  if [ "${CARCH}" = "i686" ]; then
    # https://sourceware.org/bugzilla/show_bug.cgi?id=30421
    apply_patch_with_msg decorated-symbols-in-import-libs.patch
  fi

  # https://sourceware.org/git/?p=binutils-gdb.git;a=patch;h=7e1b5887642d6c7c341c2db09ae912041d670a79
  # https://sourceware.org/git/?p=binutils-gdb.git;a=patch;h=a2243c30bc13c9adce71d70eeb5e97b7846b48f3
  # https://sourceware.org/git/?p=binutils-gdb.git;a=patch;h=0e759f232b6def277d4ae3f2d8bccfe6e34d6034
  apply_patch_with_msg \
    0001-improve-speed-of-computing-checksums-coff.patch \
    0003-make-ar-faster.patch \
    0004-add-section-caches-to-coff_data_type.patch \
    0005-add-section-caches-to-coff_data_type.patch \
    0006-add-section-caches-to-coff_data_type.patch
}

build() {
  [[ -d ${srcdir}/build-${MSYSTEM} ]] && rm -rf ${srcdir}/build-${MSYSTEM}
  mkdir -p build-${MSYSTEM} && cd build-${MSYSTEM}

  if check_option "debug" "y"; then
    CFLAGS+=" -O0"
  fi

  declare -a extra_config=""
  if [ "${CARCH}" = "x86_64" ]; then
    extra_config+=('--enable-64-bit-bfd')
  else
    LDFLAGS+=" -Wl,--large-address-aware"
  fi

  ../${_realname}-${pkgver}/configure \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --prefix=${MINGW_PREFIX} \
    --disable-werror \
    --enable-lto \
    --with-sysroot=${MINGW_PREFIX} \
    --with-libiconv-prefix=${MINGW_PREFIX} \
    ${extra_config[@]} \
    --enable-nls \
    --disable-rpath \
    --disable-multilib \
    --enable-install-libiberty \
    --enable-plugins \
    --disable-shared \
    --enable-deterministic-archives

  make
}

check() {
  cd ${srcdir}/build-${MSYSTEM}

  # unset LDFLAGS as testsuite makes assumptions about which ones are active
  # do not abort on errors - manually check log files
  make LDFLAGS="" -k check || true
}

package() {
  cd ${srcdir}/build-${MSYSTEM}
  make DESTDIR=${pkgdir} install

  # https://github.com/msys2/MINGW-packages/issues/7890
  rm "${pkgdir}${MINGW_PREFIX}/lib/bfd-plugins/libdep.a"

  find ${pkgdir}${MINGW_PREFIX}/share -type f -iname "opcodes.mo" -o -iname "bfd.mo" | xargs -rtl1 rm

  install -Dm644 ${srcdir}/${_realname}-${pkgver}/COPYING      ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING
  install -Dm644 ${srcdir}/${_realname}-${pkgver}/COPYING.LIB  ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING.LIB
  install -Dm644 ${srcdir}/${_realname}-${pkgver}/COPYING3     ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING3
  install -Dm644 ${srcdir}/${_realname}-${pkgver}/COPYING3.LIB ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING3.LIB
}
